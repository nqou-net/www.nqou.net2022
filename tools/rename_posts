#!/usr/bin/env perl
use utf8;
use strict;
use warnings;
use Path::Tiny qw(path);
use YAML qw(Load);
use Time::Moment;

my $root = path(__FILE__)->absolute->parent(2);

# 記事を取得
my $posts_dir = path($root, 'source', '_posts');
my $iterator = $posts_dir->iterator(+{recurse => 1});

# 記事から時間を取得して、名称変更
while (my $post = $iterator->()) {
    next unless $post->is_file;
    my ($yaml) = split /\n---/, $post->slurp_utf8;
    my $info = Load($yaml);
    my $date     = join('T', split(/\s/, $info->{date})) . '+09';
    my $tm       = Time::Moment->from_string($date);
    my $basename = $tm->strftime('%Y/%m/%d/%H%M%S');
    my $new_post = path($posts_dir, qq{$basename.md})->absolute;
    $new_post->parent->mkpath;
    $post->move($new_post->stringify);
}
