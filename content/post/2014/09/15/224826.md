---
date: 2014-09-15T22:48:26+09:00
draft: false
iso8601: 2014-09-15T22:48:26+09:00
tags:
  - perl
  - perl-entrance
title: Perl入学式に関するツイートをIdobataに通知するボットを作った

---

<p><a href="https://twitter.com/nqounet">@nqounet</a>です。</p>

<p>最近、<a href="https://idobata.io/">Idobata</a>というツールをPerl入学式のサポーターたちで使い始めました。</p>

<p><a href="https://idobata.io/">Idobata</a>はチャットツールなのですが、webhookを経由して色々と通知を受け取れるのでなかなか快適です。</p>

<p>しかもなかなか充実しています。</p>

<p>折角なので、Perl入学式についての発言があったら通知するようなbotを作ってみました。</p>



<h2>だいたいこんな感じ</h2>

<p>メインスクリプトの全体像は大体こんな感じです。</p>

```perl
#!/usr/bin/env perl
use utf8;
use v5.12;

use AnyEvent::Twitter::Stream;
use Encode;
use Furl;

use Config::Pit;
my $config = pit_get(
    'nqounet@twitter.com',
    require => {
        consumer_key        => 'consumer_key',
        consumer_secret     => 'consumer_secret',
        token        => 'access_token',
        token_secret => 'access_token_secret',
    }
);

my $furl = Furl->new;
my $done = AE::cv;

my $listener = AnyEvent::Twitter::Stream->new(
    method   => 'filter',
    track    => 'Perl入学式',
    on_tweet => sub {
        my $tweet = shift;
        $furl->post(
            'https://idobata.io/hook/generic/xxxxxxxxxxxxxxx',
            ['Content-Type' => 'application/x-www-form-urlencoded'],
            [source         => encode_utf8(qq{$tweet->{user}{name} : $tweet->{text}})]
        );
    },
    %{$config}
);

$done->recv;
```

<h2>まずはアプリケーションを登録</h2>

<p>TwitterはAPIが充実していますが、使う場合は登録が必要です。</p>

<ul>
<li><a href="https://apps.twitter.com/app/new">Create an application | Twitter Application Management</a></li>
</ul>

<p>登録は英語ですが、日本語の解説を読みながらだと簡単です。</p>

<ul>
<li><a href="http://syncer.jp/twitter-api-create-application">アプリケーションの登録 - Twitter APIの使い方</a></li>
</ul>

<p>アクセストークンが必要なのでアクセストークンも発行しておきます。</p>

<p>発行後、しばらく待っていると発行されてページに情報が表示されるようになります。</p>

<h2>OAuthしましょう</h2>

<p>各種トークンの読み込みは<code>Config::Pit</code>を使っていますが、パスワードを書かないようにしているだけですので、ハッシュで指定してもかまいません。</p>

<ul>
<li><a href="http://perl-users.jp/modules/config_pit.html">パスワード設定をコードに書かない(Config::Pit) - モダンなPerl入門 - モダンなPerl入門</a></li>
</ul>

<p>OAuthを使用するので、<code>Net::OAuth</code>もインストールが必要です。</p>

<h2>Payload URL</h2>

<p>Idobataで任意のwebhookを受信するためには、<code>Generic</code>を選択します。</p>

<p>設定にある<code>Endpoint URL</code>に対してデータを送信することになります。</p>

<p>通信には<code>Furl</code>を使用することにします。</p>

<p>慣れているならば<code>LWP::UserAgent</code>などでも全く問題ありません。</p>

<p>httpsでの通信になるので<code>IO::Socket::SSL</code>や<code>LWP::Protocol::https</code>もインストールが必要な場合があります。</p>

<h2>ツイートをフィルタリング</h2>

<p>ここでは<code>AnyEvent::Twitter::Stream</code>を使うことにします。</p>

<p>インスタンスを生成する時に<code>method</code>に<code>filter</code>を指定し、フィルタリングする内容を<code>track</code>に指定すると、該当するツイートが流れてきた時に、<code>on_tweet</code>が実行されます。</p>

<ul>
<li><a href="https://dev.twitter.com/streaming/reference/post/statuses/filter">POST statuses / filter | Home</a></li>
</ul>

<p>また、Twitterでは、HTTPSでの接続を推奨しているため、<code>AnyEvent::Twitter::Stream</code>のドキュメントにあるとおり<code>Net::SSLeay</code>もcpanfileに書いてインストールするようにしておきましょう。</p>

<h2>サーバーに常駐させる</h2>

<p>このまま実行するとターミナルを開けたままにしておかないといけないので、デーモン化してサーバーに常駐させます。</p>

<p>デーモン化するのは<code>App::Daemon</code>がとてもお手軽です。</p>

```
use App::Daemon qw(daemonize);
daemonize();
```

<p>これが書いてあるスクリプトで<code>start</code>,<code>stop</code>,<code>status</code>などのコマンドが使えるようになります。</p>

<p>また、デーモン化しないためには<code>-X</code>というオプションをつけるとデーモン化しないので動作確認ができます。</p>

<p>また、不意のエラーから復活させるためProcletで監視させることにしたので、デーモン化するのはProcletの方です。</p>

<p>つまり、デーモン化したProcletによってロボットを監視する構成です。</p>

<p>また、Procletでloggerを指定することで、プログラム本体はログの出力を意識する必要がなくなりますので、非常に便利です。</p>

<p>サーバーに常駐させるスクリプトを作るときには、この構成が手軽で良さそうな気がします。</p>

```perl
#!/usr/bin/env perl
use utf8;
use v5.12;

use Proclet;
use Path::Tiny qw(path cwd);
use File::RotateLogs;

use App::Daemon qw(daemonize);
daemonize();

my $logfile = join '/', cwd->absolute, 'log', 'app.log';
path($logfile)->parent->mkpath;

my $logger = File::RotateLogs->new(
    logfile      => qq{$logfile-%Y%m%d},
    linkname     => $logfile,
    rotationtime => 86400,
    maxage       => 86400 * 14,
);

my $proclet = Proclet->new(logger => sub { $logger->print(@_); });
$proclet->service(
    code => [qw(perl main.pl)],
    tag  => 'bot',
);
$proclet->run;
```

<p>このファイルも加えた結果、cpanfileはこうなりました。</p>

<p>なお、デバッグ用に<code>Data::Printer</code>も追加しています。</p>

```
requires 'perl', '5.012001';
requires 'AnyEvent::Twitter::Stream';
requires 'Furl';
requires 'Config::Pit';
requires 'Net::OAuth';
requires 'Net::SSLeay';
requires 'IO::Socket::SSL';
requires 'App::Daemon';
requires 'Proclet';
requires 'Path::Tiny';
requires 'File::RotateLogs';
requires 'Data::Printer';
```

<p><code>carton install</code>でインストールされたモジュールは98個になっていました。</p>

<p>…多いですね。</p>

<p>しばらくはこの構成で様子を見てみようと思います。</p>

<h2>参考になる資料</h2>

<ul>
<li><a href="https://metacpan.org/pod/perl">perl - The Perl 5 language interpreter - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/AnyEvent::Twitter::Stream">AnyEvent::Twitter::Stream - Receive Twitter streaming API in an event loop - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/App::Daemon">App::Daemon - Start an Application as a Daemon - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/Config::Pit">Config::Pit - Manage settings - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/Data::Printer">Data::Printer - colored pretty-print of Perl data structures and objects - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/Furl">Furl - Lightning-fast URL fetcher - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/IO%3A%3ASocket%3A%3ASSL">IO::Socket::SSL - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/Net::OAuth">Net::OAuth - OAuth 1.0 for Perl - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/Net::SSLeay">Net::SSLeay - metacpan.org</a></li>
<li><a href="https://metacpan.org/pod/Proclet">Proclet - minimalistic Supervisor - metacpan.org</a></li>
</ul>
    	