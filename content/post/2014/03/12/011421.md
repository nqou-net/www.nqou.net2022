---
date: 2014-03-12T01:14:21+09:00
draft: false
iso8601: 2014-03-12T01:14:21+09:00
tags:
  - perl
title: Mojoliciousでイベント駆動型プログラミングしてみた（その2）

---

<p><a href="https://twitter.com/nqounet">@nqounet</a>です。</p>

<p>前回に引き続き、イベント駆動型プログラミングについて考えながら書いてみました。</p>



<p>前回は、Mojo::EventEmitterのSYNOPSISから考えてみましたが、イマイチ腑に落ちないというか、しっくりこない感じで消化不良だったので、もう少し違う角度から見てみようと思います。</p>

<h2>フックとしてのイベント</h2>

<p>RPGというか、具体的にはドラゴンクエストでは、毒を受けた状態だと数歩歩くごとに毒のダメージを受けます。</p>

<p><code>歩く</code>というイベントに対して、毒の状態の場合にダメージをうけるようなプログラムを考えてみました。</p>

```perl poison
#!/usr/bin/env perl
use utf8;
use 5.012;
use Encode;

package Human;
use Mojo::Base 'Mojo::EventEmitter';

sub walk {
    my $self = shift;
    $self->emit(message => '歩いている。');
    $self->emit('walk');
    return;
}

sub get_poison {
    my $self = shift;
    $self->emit(message => '毒を受けた！');
    $self->on(walk => sub {
        $self->emit(message => "毒のダメージ！");
    });
}

sub cure_poison {
    my $self = shift;
    $self->emit(message => '毒が治った！');
    $self->unsubscribe('walk');
}

package main;

my $man = Human->new;
$man->on(message => sub {
    say Encode::encode_utf8($_[1]);
});

$man->walk for 1 ..2;
$man->get_poison;
$man->walk for 1 .. 2;
$man->cure_poison;
$man->walk for 1 .. 2;
```

<p>多少無理矢理な気もしますが。</p>

<p>歩く<code>$man->walk</code>と、<code>walk</code>というイベントが発生し、そのイベントを購読した状態が毒を受けた状態で、購読をしていない状態がステータス異常が特にない、という状態です。</p>

<p>普通（？）に書くと以下の様な感じかなと思います。</p>

```perl oop_poison
#!/usr/bin/env perl
use utf8;
use 5.012;
use Encode;

package Logger;
use Mojo::Base -base;

sub log {
    shift;
    say Encode::encode_utf8(join "\n", @_);
}

package Human;
use Mojo::Base -base;

has is_poison => 0;

sub walk {
    my $self = shift;
    my @results = ('歩いている。');
    if ($self->is_poison) {
        push @results, '毒のダメージ！';
    }
    return @results;
}

sub get_poison {
    shift->is_poison(1);
    return '毒を受けた！';
}

sub cure_poison {
    shift->is_poison(0);
    return '毒が治った！';
}

package main;

my $man = Human->new;
my $logger = Logger->new;

$logger->log($man->walk) for 1 .. 2;
$logger->log($man->get_poison);
$logger->log($man->walk) for 1 .. 2;
$logger->log($man->cure_poison);
$logger->log($man->walk) for 1 .. 2;
```

<p>Humanクラスが毒の状態を持っていて、その状態を歩く都度確認し、毒状態であればダメージ、そうでなければ何もしない、という感じです。</p>

<p>拡張性を考えれば毒の状態をHumanクラスが持つのはまずいですが、今のところはそこまで考えないことにします。</p>

<p>当初は、Humanクラスの中で<code>say</code>していたのですが、<code>say</code>が分散するのを防ごうと思った時、<code>歩く</code>というイベント自体よりも、ログの方にEventEmitterの強みを感じました。</p>

<p>通常、呼び出した先で何か異常が起きた時、それを呼び出し元で対処するのは難しいです。</p>

<p><code>EventEmitter</code>を使った場合、Humanクラスの<code>message</code>イベントを購読しておくことで、Humanクラス内で発生させた<code>message</code>イベントを呼び出し元で簡単に扱うことができます。</p>

<p>また、その際、呼び出し元に対してメッセージやオブジェクトを返したりせずに、その場でイベントを発生させれば良いのでプログラムがわかりやすくなったと感じました。</p>

<h2>エラー発生時の処理としてのイベント</h2>

<p>そこまで考えた時に、<code>try ~ catch</code>のことを思い出しました。</p>

<p><code>try</code>は、致命的なエラーが発生するかもしれないコードを実行するときに、そのエラーをトラップして異常終了しないように使います。</p>

<p>もしかすると、<code>try ~ catch</code>ではなく<code>error</code>イベントを購読してエラーをトラップする、というのがイベント駆動型の一つの使い所としてわかりやすいような気がしました。</p>
    	