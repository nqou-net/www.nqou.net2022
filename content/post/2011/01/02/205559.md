---
date: 2011-01-02T20:55:59+09:00
draft: false
iso8601: 2011-01-02T20:55:59+09:00
tags:
  - mojolicious
  - perl
title: Mojolicious::LiteでData::Modelを使ってみた

---

先日1.0にバージョンアップしたMojolicious（::Lite）を本格的に触ってみようとアレコレやってみた。
MojoliciousのWikiにORLiteを使ったサンプルがあったのですが、残念ながらそのままでは日本語には対応していないので、他のORMを使ってみようという、そんな感じです。

## SEE ALSO
- [Data::Model - model interface which had more data sources unified, a.k.a data/object mapper - metacpan.org](https://metacpan.org/pod/Data::Model)
- [Data::Model Track - JPerl Advent Calendar 2009](http://perl-users.jp/articles/advent-calendar/2009/data-model/)
- [Mojolicious::Lite - Micro real-time web framework - metacpan.org](https://metacpan.org/pod/Mojolicious::Lite)
- [Home · mojolicious/mojo Wiki](https://github.com/mojolicious/mojo/wiki)


ほとんどが元ネタのパクリですが。
```perl
#!/usr/bin/env perl
#ｕｔｆ８
# use Acme::PerlTidy;
package DataModel;
use utf8;
use base 'Data::Model';
use Data::Model::Schema;
use Data::Model::Driver::DBI;
my $dbfile = qq{$0.db};
my $dsn    = qq{dbi:SQLite:dbname=$dbfile};
my $driver = Data::Model::Driver::DBI->new(
    dsn             => $dsn,
    connect_options => {sqlite_unicode => 1,},
);
base_driver($driver);
install_model motorcycles => schema {
    key 'id';
    column id    => int  => {auto_increment => 1};
    column type  => char => {required       => 1};
    column brand => char => {required       => 1};
    column color => char => {required       => 0};
};
unless (-f $dbfile) {
    my $dbh = DBI->connect($dsn, '', '', {RaiseError => 1, PrintError => 0})
        or DBI->errstr;
    for my $sql (__PACKAGE__->as_sqls) {
        $dbh->do($sql) or die $dbh->errstr;
    }
    $dbh->disconnect;
}

package main;
use utf8;
use Mojolicious::Lite;
use Mojo::Util qw/md5_sum/;
app->log->level('debug')->path(qq{$0.log});
app->secret(md5_sum $0 )->log->debug(app->secret);
app->helper(model => sub { my $dbh = DataModel->new });
get '/' => sub {
    my $self       = shift;
    my $model      = $self->model;
    my $motorbikes = [$model->get('motorcycles' => {order => [{type => 'ASC'}],})];
    $self->stash(
        motorbikes => $motorbikes,
        debug      => $self->dumper([$self, $model]),
    );
}        => 'index';
post '/' => sub {
    my $self  = shift;
    my $model = $self->model;
    $model->set(
        'motorcycles' => {
            type  => $self->param('type'),
            brand => $self->param('brand'),
            color => $self->param('color'),
        }
    );
    $self->redirect_to('/');
};
app->start;
__DATA__
@@ index.html.ep
<!DOCTYPE html>
<html>
<head>
<meta charset="<%= app->renderer->encoding %>">
<title>Motorcycles</title>
</head>
<body>
<table>
% foreach my $cycle (@{$motorbikes} ) {
<tr>
<td><%= $cycle->id %></td>
<td><%= $cycle->type %></td>
<td><%= $cycle->brand %></td>
<td><%= $cycle->color %></td>
</tr>
% }
</table>
<p>バイクを登録してください。</p>
<p>
<%= form_for '/' => (method => 'post') => begin %>
% foreach (qw/type brand color/) {
<%= uc($_) %>: <%= input_tag $_, 'type' => 'text' %><br />
% }
<%= submit_button 'Submit motorcycle' %>
<% end %>
</p>
<pre><%= $debug %></pre>
</body>
</html>
```
    	