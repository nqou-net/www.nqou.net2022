---
date: 2015-05-14T09:58:42+09:00
draft: false
iso8601: 2015-05-14T09:58:42+09:00
tags:
  - perl
title: Resqueでメッセージキューをやってみた

---

<p><a href="https://twitter.com/nqounet">@nqounet</a>です。</p>

<p><code>Perl</code>で、<code>Redis</code>を使ったメッセージキューシステムの<code>Resque</code>というモジュールを使ってみたのでメモしておきます。</p>



<h3>前置き</h3>

<p>沢山のリクエストを処理するようなウェブアプリでは、少し時間のかかる処理をジョブキューやメッセージキューというようなキューシステムを使って処理することが多くなるでしょう。</p>

<p>個人的にはQudoが好きでよく使っているのですが、NoSQLを使ったものはないのだろうかと探してみたら、Resqueというモジュールを見つけたので使ってみました。</p>

<h3>ファイル群</h3>

<p>キューシステムは、シンプルにしてもファイル数が多くなるので、慣れないうちは理解するのが大変だと思いますが、コレも慣れです。</p>

```
├── client.pl
├── cpanfile
├── lib
│   └── MyTask
│       └── Echo.pm
├── main.pl
└── worker.pl
```

<h3>それぞれの役目</h3>

<h4>cpanfile</h4>

<p><code>cpanfile</code>には、モジュールの依存情報、平たく言うと使っているモジュールを書いておきます。</p>

<p>このファイルを作っておくと、モジュールのインストールが簡単です。</p>

<p>cartonを使わない場合でも、<code>cpanm --installdeps .</code>のようにすると、依存モジュールをインストールしてくれます。</p>

```
requires 'Log::Minimal';
requires 'Path::Tiny';
requires 'Proclet';
requires 'Resque';
requires 'Test::RedisServer';
```

<h4>client.pl</h4>

<p><code>client.pl</code>は、キューを作成する、平たく言うと仕事を取ってくる役目を担います。</p>

<p>会社で言うと、営業の方ですかね。仕事を取ってきて、キューに入れるところまでが役目です。</p>

<p>書いていてちょっと気になったのは、<code>args</code>が配列のリファレンスしか受け付けないことです。</p>

<p>こういう時は、なんとなくハッシュリファレンスを渡したくなるのですが、受け付けてくれません。</p>

```
#!/usr/bin/env perl
use utf8;
use strict;
use warnings;
use Resque;
use Log::Minimal;


my $resque = Resque->new;

my $count = 10;
while ($count--) {
    $resque->push(
        echo => +{
            class => 'MyTask::Echo',
            args  => +[+{count => $count}]
        }
    );
    infof($count);
    sleep 2;
}
```

<h4>worker.pl</h4>

<p><code>worker.pl</code>は、キューを処理する、平たく言うと実際に仕事を片付けるのを担当します。</p>

<p>キューに仕事がある限り、順番に処理するのが役目です。</p>

<p><code>use lib 'lib';</code>しているのはちゃんと理由があります。</p>

<p><code>worker.pl</code>は、ワーカーのクラスを自動的にロードして使ってくれるのですが、今回のディレクトリの構成では、<code>lib</code>の中にワーカーのクラス（<code>MyTask::Echo</code>）があるので、予めライブラリが<code>lib</code>にあることを書いておきます。</p>

```
#!/usr/bin/env perl
use utf8;
use strict;
use warnings;
use Resque;
use Log::Minimal;
use lib 'lib';


my $worker = Resque->new->worker;

$worker->add_queue('echo');
infof('### start worker');
$worker->work;
infof('### stop worker');
```

<h4>lib/MyTask/Echo.pm</h4>

<p><code>Echo.pm</code>は、実際の処理の内容です。</p>

<p><code>client.pl</code>で、指定している<code>MyTask::Echo</code>は、このファイルの事を指しています。</p>

<p><code>worker.pl</code>は、このファイルのような仕事の手順書があれば、どのような処理でも実行できます。</p>

<p>なお、<code>perform</code>は、<code>worker.pl</code>が呼び出す関数名です。</p>

```
package MyTask::Echo;
use utf8;
use strict;
use warnings;
use Log::Minimal;


sub perform {
    my $job = shift;
    debugf('dequeue: %s', ddf($job->args));
    infof($job->args->[0]{count});
}

1;
```

<h4>main.pl</h4>

<p><code>Proclet</code>を使って、<code>client.pl</code>と<code>worker.pl</code>と<code>Redis</code>を同時に動かします。</p>

<p><code>worker.pl</code>を複数動作させたい場合は、<code>Proclet</code>のworkerの数を変更するだけなので、いろいろ簡単に試せて便利ですね。</p>

```
#!/usr/bin/env perl
use utf8;
use strict;
use warnings;
use Path::Tiny qw(path tempdir);
use Test::RedisServer;
use Proclet;


my $redis = Test::RedisServer->new(
    auto_start => 0,
    tmpdir     => tempdir(),
);
$ENV{REDIS_SERVER} = $redis->connect_info;

my $proclet = Proclet->new(color => 1);
$proclet->service(
    code => sub {
        $redis->exec;
    },
    worker => 1,
    tag    => 'redis'
);
$proclet->service(
    code   => [qw{carton exec -- perl worker.pl}],
    worker => 5,
    tag    => 'worker'
);
$proclet->service(
    code   => [qw{carton exec -- perl client.pl}],
    worker => 1,
    tag    => 'client'
);
$proclet->run;
```

<h3>使い方</h3>

<p>当然ですが、Resqueを試すにはRedisをインストールしておく必要があります。</p>

<p>Macでhomebrewを使っている方は、ターミナルから<code>brew install redis</code>と入力するだけです。</p>

<p>その他の方は、Redisを入れるか、homebrewを入れるか、Macを買うかしてください。</p>

<p>全部コピペしたら、<code>cpanfile</code>のあるディレクトリで、<code>carton install</code>と入力してください。</p>

<p>必要なモジュールがインストールされます。</p>

<p><code>carton</code>が無い場合は、その前に<code>cpanm Carton</code>と入力してください。</p>

<p><code>cpanm</code>が無い場合は、その前に<code>curl -L https://cpanmin.us | perl - App::cpanminus</code>と入力してください。</p>

<p>モジュールのインストールが終わったら、<code>carton exec -- perl main.pl</code>と入力してみましょう。</p>

<p>勝手に色々動き始めます。</p>

<p>ジョブキューが初めての方は、<code>main.pl</code>に書いてある<code>worker</code>や<code>client</code>の<code>worker</code>を増減してみたり、<code>client.pl</code>の<code>sleep</code>を消したり、数値を変更したり、色々試してみてください。</p>

```
$ brew install redis
$ carton install
$ carton exec -- perl main.pl
```

<h3>使ってみての感想</h3>

<p>RDBMSの場合、少なくともテーブル、場合によってはユーザーやデータベースなどを作る必要があり、ちょっと触ってみるにしても結構面倒なところがあります。</p>

<p>Qudoは、そういう部分のヘルパーもあり、楽チンではあるのですが。</p>

<p>Redis＋Resqueの場合は、そのあたりが何も必要ないので、とても簡単です。</p>

<p>ちょっと使ってみたり、ジョブキューに触れて見るにはちょうど良いと思います。</p>

<h3>参考資料</h3>

<ul>
<li><a href="https://metacpan.org/pod/Resque">Resque - Redis-backed library for creating background jobs, placing them on multiple queues, and processing them later. - metacpan.org</a></li>
<li><a href="http://perl-users.jp/articles/advent-calendar/2012/casual/12">Resque で学ぶジョブキューイング - Perl Advent Calendar Japan 2012 Casual Track</a></li>
<li><a href="https://metacpan.org/pod/Qudo">Qudo - simple and extensible job queue manager - metacpan.org</a></li>
<li><a href="http://gihyo.jp/dev/serial/01/perl-hackers-hub/001001">第10回　ジョブキューで後回し大作戦―TheSchwartz，Qudo，Q4M（1）：Perl Hackers Hub｜gihyo.jp … 技術評論社</a></li>
</ul>
    	